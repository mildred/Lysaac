Section Header

  + name := ITM_TYPE;

Section Inherit

  + parent_itm :Expanded ITM;

Section Public

  + prototype :ITM_EXPR_PROTO;
  
  + block :BOOLEAN;
  
  - to_type :TYPE <-
  // Return NULL if the type cannot be found
  ( + res :TYPE;
  
    block.if {
      + p:PROTOTYPE;
      p := cluster_parent.prototype_for_role "Block";
      (p = NULL).if {
        err := error(err, position, "Could not find prototype for role Block");
      } else {
        res := p.to_type_block;
      };
    } else {
      res := slot_parent.find_type (prototype.prototype);
      (res = NULL).if {
        err := error(err, position, "Could not find type " + name);
      };
    };
    res
  )
  [ +? { (err.failure) || {Result != NULL} }; ];
  
  + err :ERR := ERR;
  
  - name :CSTRING <- prototype.prototype;

Section PARSER
  
  - make_block :SELF <-
  (
    prototype := NULL;
    block     := TRUE;
    Self
  );
  
  - make p:ITM_EXPR_PROTO :SELF <-
  (
    prototype := p;
    block     := FALSE;
    Self
  );

