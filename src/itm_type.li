Section Header

  + name := ITM_TYPE;

Section Inherit

  + parent_itm :Expanded ITM;

Section Public

  + prototype :ITM_EXPR_PROTO;
  
  + block :BOOLEAN;
  
  - to_type :MAYBE(TYPE) <-
  ( + res :MAYBE(TYPE);
  
    block.if {
      + p:PROTOTYPE;
      p := cluster_parent.prototype_for_role "Block";
      (p = NULL).if {
        res <! error(ERR, position, "Could not find prototype for role Block");
      } else {
        res << p.to_type_block;
      };
    } else {
      res << slot_parent.find_type (prototype.prototype);
      (res.element = NULL).if {
        res <! error(ERR, position, "Could not find type " + name);
      };
    };
    res
  );
  
  - name :CSTRING <- prototype.prototype;

Section PARSER
  
  - make_block :SELF <-
  (
    prototype := NULL;
    block     := TRUE;
    Self
  );
  
  - make p:ITM_EXPR_PROTO :SELF <-
  (
    prototype := p;
    block     := FALSE;
    Self
  );

