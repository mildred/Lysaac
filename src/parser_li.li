Section Header

  + name      := PARSER_LI;

Section Inherit

  + parent_parser :Expanded PARSER;

Section SELF

  + prototype :PROTOTYPE;

  - read_li <-
  (
    read_section_header;
    read_sections;
  );
  
  - hook_h_slot (ident:CSTRING, style:ITM_STYLE, expr:ITM_EXPR) <-
  (
    (ident == "name").if {
      expr.is_a ITM_EXPR_PROTO.if { + p:ITM_EXPR_PROTO;
        p ?= expr;
        (p.prototype.as_lower == prototype.name).if_false {
          print_warning;
          "expected ".print; prototype.name.as_upper.println;
        };
      } else {
        print_warning;
        "expected prototype constant".println;
      };
    } else {
      print_warning;
      "unknown slot ".print; ident.println;
    };
  );

Section PROTOTYPE

  - make p:PROTOTYPE :SELF <-
  (
    prototype := p;
    Self
  );
  
  - parse <-
  (
    open_source (prototype.filename);
    read_li;
  );
