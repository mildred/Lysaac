Section Header

  + name      := PARSER_LI;

Section Inherit

  + parent_parser :Expanded PARSER;

Section SELF

  + prototype :PROTOTYPE;
  
  + ast :ITM_PROTOTYPE;

  - read_li <-
  (
    read_section_header;
    read_sections;
  );
  
  - hook_header_slot slot:ITM_SLOT <-
  (
    (slot.identifier == "name").if {
      slot.expression.is_a ITM_EXPR_PROTO.if { + p:ITM_EXPR_PROTO;
        p ?= slot.expression;
        (p.prototype == prototype.name).if_false {
          warning ("expected "+prototype.name);
        };
      } else {
        warning("expected prototype constant");
      };
    }.elseif {slot.identifier == "type"} then {
      slot.has_keyword "Integer".if {
        slot.expression.is_a ITM_EXPR_INT.if {
          + p:ITM_EXPR_INT;
          p ?= slot.expression;
          prototype.set_base_type (BASE_TYPE.create_integer (p.value));
        } else {
          warning("expected integer constant");
        };
      };
    }.elseif {slot.identifier == "role"} then {
      slot.expression.is_void.if_false {
        warning("expected void expression");
      };
      slot.has_keyword "String".if {
        + p :PROTOTYPE;
        p := prototype.cluster.check_prototype_for_role "String";
        (p = NULL).if {
          prototype.set_role "String";
        } else {
          p.set_role NULL;
          warning("There is already a prototype with the role String in "+p.filename);
        };
      };
    } else {
      warning("unknown slot "+slot.identifier);
    };
  );

  - handle_slot slot:ITM_SLOT <-
  (
    ast.add_slot slot;
  );

Section PROTOTYPE

  - make p:PROTOTYPE :SELF <-
  (
    prototype := p;
    ast := ITM_PROTOTYPE.clone.make p;
    Self
  );
  
  - parse :ITM_PROTOTYPE <-
  (
    open_source (prototype.filename);
    read_li;
    ast
  );
