Section Header

  + name := Expanded ERR;

Section Insert

  + parent_any :Expanded ANY;

Section Public

  + position :POS            := POS;
  + message  :CSTRING        := "";
  + parent   :REFERENCE(ERR) := NULL;
  
  - failure :BOOLEAN <- ! success;
  
  - success :BOOLEAN <-
    (position.is_null) &&
    {message.is_empty} &&
    {(parent = NULL) || {parent.to_e.success}};
    
  - assert <-
  (
    failure.if {
      crash_with_message to_string;
    };
  );
  
  - if_fail blc:{SELF;} :SELF <-
  (
    failure.if { blc.value Self; };
    Self
  );
  
  - if_fail2 blc:{REFERENCE(SELF);} :SELF <-
  (
    failure.if { blc.value (REFERENCE(SELF).create Self); };
    Self
  );
  
  - to_string :STRING <-
  [ -? { ERR.success }; ]
  ( + res:STRING;
    res := STRING.create 128;
    to_string_in res;
    res
  )
  [ +? { ERR.success }; ];
  
  - print   <- warn   to_string;
  - println <- warnln to_string;
  
  - create msg:CSTRING :ERR <-
    [ -? { ERR.success }; ]
    ( + e:ERR; e := clone; e.make msg position POS parent NULL; e )
    [ +? { ERR.success }; ];

  - create msg:CSTRING parent     p:ERR :ERR <-
    [ -? { ERR.success }; ]
    ( + e:ERR; e := clone; e.make msg position POS parent (REFERENCE(ERR).create p); e )
    [ +? { ERR.success }; ];

  - create msg:CSTRING position pos:POS :ERR <-
    [ -? { ERR.success }; ]
    ( + e:ERR; e := clone; e.make msg position pos parent NULL; e )
    [ +? { ERR.success }; ];

  - create msg:CSTRING position pos:POS parent p:ERR :ERR <-
    [ -? { ERR.success }; ]
    ( + e:ERR; e := clone; e.make msg position pos parent (REFERENCE(ERR).create p); e )
    [ +? { ERR.success }; ];
    
  - clone :SELF <-
  // Very ugly hack !!!
  ( + e:SELF;
    + r:REFERENCE(SELF);
    r := REFERENCE(SELF).create SELF;
    e := r.to_e;
    e
  );

Section ERR

  - to_string_in res:STRING <-
  (
    (parent != NULL).if {
      parent.to_e.to_string_in res;
      parent.to_e.failure.if {
        res.add_last '\n';
      };
    };
    failure.if {
      position.is_null.if_false {
        res.append (position.to_string);
        res.append ": ";
      };
      res.append message;
    };
  );

Section Private

  - make msg:CSTRING position pos:POS parent p:REFERENCE(ERR) :ERR <-
  [ -? { ERR.success }; ]
  (
    message  := msg;
    position := pos;
    parent   := p;
    Self
  )
  [ +? { ERR.success }; ];

