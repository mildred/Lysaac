Section Header

  + name := GENERATOR;

Section Inherit

  + parent_any :Expanded ANY;

Section Public

  - type_integer size:INTEGER <-
  (
    context.add_last 'i';
    size.append_in code;
  );

  - type_pointer blc:{} <-
  (
    blc.value;
    context.add_last '*';
  );
  
  - type_function_pointer <-
  (
    put "void () *";
  );

  - declare_string string:ABSTRACT_STRING in var:VAR <-
  (
    context_const {
      var.realize "@" id get_global;
      put_var var;
      put " = private constant [";
      put_int (string.count+1);
      put " x i8] c\"";
      string.lower.to (string.upper) do { i:INTEGER;
        + c:CHARACTER;
        c := string.item i;
        c.code.in_range 32 to 127.if {
          put_c c;
        } else {
          put_c '\\';
          c.to_hexadecimal_in const;
        };
      };
      put "\\00\"\n";
    };
  );
  
  - get_string_pointer_from from:VAR string s:ABSTRACT_STRING to to:VAR <-
  (
    to.realize "%" id get_local;
    put_indent;
    put_var to;
    put " = getelementptr [";
    put_int (s.count+1);
    put " x i8]* ";
    put_var from;
    put ", i32 0, i32 0\n";
  );

  - declare_sub name:CSTRING args args:{} <-
  (
    context.append indent_str;
    context.append "declare void @";
    context.append name;
    context.append " (";
    args.value;
    context.append ")\n\n";
  );
  
  - sub name:CSTRING self s:VAR args args:{} do blc:{} <-
  (
    s.realize "null";
    context.append indent_str;
    context.append "define void @";
    context.append name;
    context.append " (";
    args.value;
    context.append ") {\n";
    last_local := 0;
    indent;
    blc.value;
    context.append indent_str;
    context.append "ret void\n";
    unindent;
    context.append indent_str;
    context.append "}\n\n";
  );
  
  - call name:CSTRING args blc:{} <-
  (
    context.append indent_str;
    context.append "tail call void @";
    context.append name;
    context.append "(";
    blc.value;
    context.append ")\n";
  );
  
  - arg blc:{} <-
  (
    (context.last = '(').if_false {
      context.append ", ";
    };
    blc.value;
  );
  
  - call_arg var:VAR type t:{} <-
  (
    arg {
      t.value;
      context.add_last ' ';
      put_var var in code;
    };
  );
  
  - context_code blc:{} <-
  ( + old:STRING;
    old := context;
    context := code;
    blc.value;
    context := old;
  );
  
  - context_const blc:{} <-
  ( + old:STRING;
    old := context;
    context := const;
    blc.value;
    context := old;
  );

Section LYSAAC

  - output :STRING <- const + "\n\n" + code + "\n";

Section Private

  + last_global :INTEGER := 0;
  - get_global :INTEGER <-
  (
    last_global := last_global + 1;
    last_global
  );

  + last_local :INTEGER := 0;
  - get_local :INTEGER <-
  (
    last_local := last_local + 1;
    last_local
  );

  + const :STRING := STRING.create 1024;
  + code  :STRING := STRING.create 1024;
  + context :STRING <- code;
  
  - indent   <- (indent_level := indent_level + 1;);
  - unindent <- (indent_level := indent_level - 1;);
  + indent_level :INTEGER := 0;
  - indent_str :ABSTRACT_STRING <-
  ( + res :ABSTRACT_STRING;
    (indent_level == 0).if {
      res := "";
    }.elseif {indent_level == 1} then {
      res := "  ";
    }.elseif {indent_level == 2} then {
      res := "    ";
    }.elseif {indent_level == 3} then {
      res := "      ";
    }.elseif {indent_level == 4} then {
      res := "        ";
    }.elseif {indent_level == 5} then {
      res := "          ";
    }.elseif {indent_level == 6} then {
      res := "            ";
    }.elseif {indent_level == 7} then {
      res := "              ";
    } else {
      res := "                ";
    };
    res
  );
  
  - put s:ABSTRACT_STRING <- context.append s;
  - put_c c:CHARACTER     <- context.add_last c;
  - put_int i:INTEGER     <- i.append_in context;
  - put_indent            <- put indent_str;
  - put_var var:VAR       <- put_var var in context;
  
  - put_var var:VAR in str:STRING <-
  (
    str.append (var.name);
    (var.name == "@").if {
      ? { var.identifier > 0 };
      (var.identifier - 1).append_in str;
    }.elseif {var.name == "%"} then {
      ? { var.identifier > 0 };
      var.identifier.append_in str;
    };
  );


