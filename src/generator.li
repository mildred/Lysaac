Section Header

  + name := GENERATOR;

Section Inherit

  + parent_any :Expanded ANY;

Section Private

  + last_var :INTEGER := 0;
  - get_var :INTEGER <-
  (
    last_var := last_var + 1;
    last_var
  );

  + const :STRING := STRING.create 1024;
  + decl  :STRING := STRING.create 1024;
  + code  :STRING := STRING.create 1024;
  
  - output :STRING <- const + "\n\n" + decl + "\n\n" + code + "\n";
  
  - indent   <- (indent_level := indent_level + 1;);
  - unindent <- (indent_level := indent_level - 1;);
  + indent_level :INTEGER := 0;
  - indent_str :ABSTRACT_STRING <-
  ( + res :ABSTRACT_STRING;
    (indent_level == 0).if {
      res := "";
    }.elseif {indent_level == 1} then {
      res := "  ";
    }.elseif {indent_level == 2} then {
      res := "    ";
    }.elseif {indent_level == 3} then {
      res := "      ";
    }.elseif {indent_level == 4} then {
      res := "        ";
    }.elseif {indent_level == 5} then {
      res := "          ";
    }.elseif {indent_level == 6} then {
      res := "            ";
    }.elseif {indent_level == 7} then {
      res := "              ";
    } else {
      res := "                ";
    };
    res
  );
  
  - put_var var:VAR in str:STRING <-
  (
    str.append (var.name);
    (var.identifier != 0).if {
      (var.identifier - 1).append_in str;
    };
  );

Section Public

  - declare_string string:ABSTRACT_STRING in var:VAR <-
  (
    var.realize "@" id get_var;
    put_var var in const;
    const.append " = ";
    const.append "private constant [";
    (string.count+1).append_in const;
    const.append " x i8] c\"";
    string.lower.to (string.upper) do { i:INTEGER;
      + c:CHARACTER;
      c := string.item i;
      c.code.in_range 32 to 127.if {
        const.add_last c;
      } else {
        const.add_last '\\';
        c.to_hexadecimal_in const;
      };
    };
    const.append "\\00\"\n";
    
  );

  - declare_sub name:CSTRING <-
  (
    decl.append indent_str;
    decl.append "declare void @";
    decl.append name;
    decl.append " ()\n";
  );
  
  - sub name:CSTRING do blc:{} <-
  (
    declare_sub name;
    code.append indent_str;
    code.append "define void @";
    code.append name;
    code.append " () {\n";
    indent;
    blc.value;
    code.append indent_str;
    code.append "ret void\n";
    unindent;
    code.append indent_str;
    code.append "}\n";
  );
  
  + call_has_arg :BOOLEAN;
  
  - call name:CSTRING args blc:{} <-
  (
    code.append indent_str;
    code.append "tail call void @";
    code.append name;
    code.append "(";
    call_has_arg := FALSE;
    blc.value;
    code.append ")\n";
  );
  
  - call_arg var:VAR <-
  (
    call_has_arg.if {
      code.append ", ";
    };
    call_has_arg := TRUE;
    put_var var in code;
  );


