Section Header

  + name := TYPE;
  
  - export := BASE_TYPE;

Section Inherit

  + parent_any :Expanded ANY;
  
Section Public

  + prototype :PROTOTYPE;
  
  + block :BOOLEAN;
  
  + base_type :BASE_TYPE;
  
  - void :BOOLEAN <- base_type.void;

  - generate gen:GENERATOR <-
  (
    (base_type = NULL).if {
      crash_with_message ("Cannot generate type for "+prototype.name);
      not_yet_implemented;
    } else {
      base_type.generate gen;
    };
  );
  
  - to_string :STRING <- type_name + "<" + prototype.name + "," + base_type.to_string + ">";
  
  - to_base_type :BASE_TYPE <- base_type;

Section PROTOTYPE

  - make p:PROTOTYPE :SELF <-
  [ -? { ! p.void }; ]
  (
    (p.role == "Block").if {
      make_block p;
    } else {
      block := FALSE;
      prototype := p;
      prototype.expanded.if {
        base_type := prototype.base_type;
      } else {
        base_type := BASE_TYPE.create_pointer (prototype.base_type);
      };
    };
    Self
  )
  [ +? { ! base_type.void }; ];
  
  - make_block p:PROTOTYPE :SELF <-
  [ -? { ! p.base_type.void }; ]
  (
    block := TRUE;
    prototype := p;
    base_type := BASE_TYPE.create_function_pointer;
    Self
  )
  [ +? { ! base_type.void }; ];

