Section Header

  + name := TYPE;
  
  - export := BASE_TYPE;

Section Inherit

  + parent_any :Expanded ANY;
  
Section Public

  + prototype :PROTOTYPE;
  
  + block :BOOLEAN;
  
  + base_type :BASE_TYPE <-
  [ -? { prototype.defined }; ]
  (
    block.if {
      base_type := BASE_TYPE.create_pointer (BASE_TYPE.create_function);
    } else {
      prototype.expanded.if {
        base_type := prototype.base_type;
      } else {
        base_type := BASE_TYPE.create_pointer (prototype.base_type);
      };
    };
    base_type
  );
  
  - void :BOOLEAN <- base_type.void;

  - generate gen:GENERATOR <-
  (
    (base_type = NULL).if {
      crash_with_message ("Cannot generate type for "+prototype.name);
      not_yet_implemented;
    } else {
      base_type.generate gen;
    };
  );
  
  - to_string :STRING <- type_name + "<" + prototype.name + "," + base_type.to_string + ">";
  
  - to_base_type :BASE_TYPE <- base_type;

Section PROTOTYPE

  - make p:PROTOTYPE :SELF <-
  [ -? { p.made }; ]
  (
    (p.role == "Block").if {
      make_block p;
    } else {
      block := FALSE;
      prototype := p;
    };
    Self
  );
  
  - make_block p:PROTOTYPE :SELF <-
  [ -? { p.made }; ]
  (
    block := TRUE;
    prototype := p;
    Self
  );

