Section Header

  + name := BASE_TYPE;

Section Inherit

  + parent_any :Expanded ANY;

Section Public

  - to_string:STRING <-
  ( + res :STRING;
    res := type_name + "<" + i_size.to_string + "," + f_ptr.to_string;
    (p_type = NULL).if {
      res.append ",NULL";
    } else {
      res.append ",";
      res.append (p_type.to_string);
    };
    (m_struct = NULL).if {
      res.append ",NULL";
    } else {
      res.append ",{";
      m_struct.iterate.loop_v { t:BASE_TYPE;
        res.append (t.to_string);
        res.add_last ',';
      };
      (m_struct.count > 0).if {
        res.remove_last 1;
      };
      res.append "}";
    };
    res.append ">";
    res
  );

  - integer :BOOLEAN <- (i_size > 0);
  - integer_size :INTEGER <- [ -?{integer}; ] i_size;
    
  - pointer :BOOLEAN <- (p_type != NULL);
  - pointer_type :BASE_TYPE <- [ -?{pointer}; ] p_type;
  
  - function_pointer :BOOLEAN <- f_ptr;
  
  - struct :BOOLEAN <- (m_struct != NULL);
  - struct_types :FAST_ARRAY(BASE_TYPE) <- [ -? {struct}; ] m_struct;
  
  - void :BOOLEAN <- {! integer} && {! pointer} &&  {! function_pointer} &&  {! struct};
  
  - i8  :BASE_TYPE := create_integer 8;
  - i16 :BASE_TYPE := create_integer 16;
  - i32 :BASE_TYPE := create_integer 32;
  - i64 :BASE_TYPE := create_integer 64;
  
  - create_integer size:INTEGER  :SELF <- clone.make_integer size;
  - create_pointer ptr:BASE_TYPE :SELF <- clone.make_pointer ptr;
  - create_function_pointer      :SELF <- clone.make_function_pointer;
  - create_struct                :SELF <- clone.make_struct;
  
  - generate gen:GENERATOR <-
  [ -? { Self != SELF }; ]
  (
    integer.if {
      gen.type_integer integer_size;
    }.elseif {pointer} then {
      gen.type_pointer {
        pointer_type.generate gen;
      };
    }.elseif {function_pointer} then {
      gen.type_function_pointer;
    }.elseif {struct} then {
      gen.type_struct {
        struct_types.iterate.loop_v { t:BASE_TYPE;
          gen.type_struct_element { t.generate gen }
        };
      };
    } else {
      gen.type_void;
    };
  );
  
  - realize_default_value gen:GENERATOR for reg:REG <-
  (
    gen.expr_undef_in reg
  );
  
  - Self:SELF '==' other:E :BOOLEAN <-
  (
    (E = SELF) &&
    {i_size   = other.i_size} &&
    {p_type   = other.p_type} &&
    {f_ptr    = other.f_ptr } &&
    {(m_struct = other.m_struct) || {m_struct == other.m_struct}}
  );

Section Private

  + i_size   :INTEGER               := 0;
  + p_type   :BASE_TYPE             := NULL;
  + f_ptr    :BOOLEAN               := FALSE;
  + m_struct :FAST_ARRAY(BASE_TYPE) := NULL;
  
  - make_integer size:INTEGER :SELF <-
  (
    i_size := size;
    p_type := NULL;
    f_ptr  := FALSE;
    m_struct := NULL;
    Self
  );
  
  - make_pointer ptr:BASE_TYPE :SELF <-
  (
    i_size := 0;
    p_type := ptr;
    f_ptr  := FALSE;
    m_struct := NULL;
    Self
  );
  
  - make_function_pointer :SELF <-
  (
    i_size := 0;
    p_type := NULL;
    f_ptr  := TRUE;
    m_struct := NULL;
    Self
  );
  
  - make_struct :SELF <-
  (
    i_size := 0;
    p_type := NULL;
    f_ptr  := FALSE;
    m_struct := FAST_ARRAY(BASE_TYPE).create 0;
    Self
  );
