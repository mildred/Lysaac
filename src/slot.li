Section Header

  + name := SLOT;

Section Inherit

  + parent_any :Expanded ANY;

Section Public

  - name :CSTRING <- ast.identifier;
    
  + list :LIST;
  
  - external :BOOLEAN <- ast.is_external;
  
  + external_name :CSTRING <- name;
  
  + argument_type :TYPE;
  
  + prototype :PROTOTYPE;

Section ITM_SLOT

  - make s:ITM_SLOT :SELF <-
  (
    list := LIST.clone.make Self;
    ast := s;
    (ast.argument_type = NULL).if {
      argument_type := NULL;
    } else {
      argument_type := ast.argument_type.to_type;
    };
    prototype := ast.prototype_parent;
    self := VAR.clone.make NULL static_type (prototype.to_type);
    ast.prototype_parent.add_slot Self;
    Self
  );

Section LIST_SLOT

  - compile <-
  (
    external.if {
      + sym :ITM_EXPR_SYM;
      sym ?= ast.expression;
      ? { sym != NULL };
      external_name := sym.symbol;
    }.elseif {ast.expression != NULL} then {
      ast.expression.compile_in list;
    };
  );
  
  - link <-
  (
    external.if_false {
      list.link;
    };
  );
  
  - generate gen:GENERATOR <-
  (
    external.if {
      gen.declare_sub external_name args {
        (argument_type != NULL).if {
          gen.arg { argument_type.generate gen; };
        };
      };
    } else {
      gen.sub external_name self self args {} do {
        list.generate gen;
      };
    };
  );

Section LIST

  + self :VAR;

Section Private

  + ast :ITM_SLOT;

