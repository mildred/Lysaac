Section Header

  + name := SLOT;

Section Inherit

  + parent_proc :Expanded PROC;

Section Public

  - name :CSTRING <- ast.identifier;
  
  - external :BOOLEAN <- ast.is_external;
  - export   :BOOLEAN <- ast.is_export;
  - internal :BOOLEAN <- ast.is_internal;
  
  + internal_opcode :CSTRING := "";
  
  + external_name :CSTRING <-
  ( 
    (export | external).if {
      external_name := name;
    } else {
      external_name := symbol_encode_prototype prototype slot Self;
    };
    external_name
  );
  
  + arguments :FAST_ARRAY(REG);
  
  + prototype :PROTOTYPE;

  + self :EXPR;
  
  + result_type :TYPE;
  
  - style :ITM_STYLE <- ast.style;
  
  - has_result :BOOLEAN <- (result_type != NULL);
  
  - to_string :STRING <-
  ( + res:STRING;
    res := type_name + "<" + name;
    (result_type != NULL).if {
      res.append (":" + result_type.to_string + ">");
    };
    res.append ">";
    res
  );
  
  + type_index :INTEGER := -1;
  
  - position :POS <- ast.position;
  
  - shared :BOOLEAN <- style.minus;
  
  - slot_data :BOOLEAN <- (ast.affect.data);
  
  - slot_code :BOOLEAN <- (! slot_data);

Section Private

  + list :LIST;

Section PROTOTYPE

  + default_value :EXPR <-
  (
    (result_type = NULL).if {
      default_value := EXPR;
    } else {
      default_value := EXPR.clone.make_type result_type;
    };
    default_value
  );

Section ITM_SLOT

  - make s:ITM_SLOT :SELF <-
  (
    make_error_collector ("slot " + s.identifier);
    arguments := FAST_ARRAY(REG).create 0;
    list          := NULL;
    ast           := s;
    prototype     := ast.prototype_parent;
    self          := EXPR;
    type_index    := -1;
    result_type   := NULL;
    ? {prototype != NULL};
    Self
  );
  
  - add_argument v:REG <- arguments.add_last v;
  
  - set_return_type t:TYPE <-
  [ -? { t != NULL }; ]
  (
    result_type := t;
  );
  
  - finish_creation <-
  ( + error :BOOLEAN;
    (internal).if {
      + sym :ITM_EXPR_SYM;
      sym ?= ast.expression;
      (sym = NULL).if {
        error := TRUE;
        add_error(ast.expression.position.error("Expected symbol"));
      } else {
        internal_opcode := sym.symbol;
      };
    };
    error.if_false {
      prototype.add_slot Self;
    };
  );

Section LIST_PROC

  - compile list_procs:LIST_PROC <-
  ( + t:TYPE;
    t := prototype.to_type;
    t.void.if {
      self := EXPR;
    } else {
      self := EXPR.clone.make_type t;
    };
    ? { self.is_valid };
    list := LIST.clone.make_self self.make_error_collector Self type result_type;
    external.if {
      // External Slot
      + sym :ITM_EXPR_SYM;
      sym ?= ast.expression;
      ? { sym != NULL };
      external_name := sym.symbol;
    } else {
      ((shared) && {slot_data}).if {
        shared_data_variable := REG.clone.make_type (BASE_TYPE.create_pointer(result_type.base_type));
      };
      list.set_list_procs list_procs;
      prototype.initializer_list.set_list_procs list_procs;
      compile_initializer;
      compile_slot;
    };
  );

Section Private

  - compile_initializer <-
  [ -? {! external}; ]
  ( + r:REG;
    slot_data.if {
      ast.expression.compile_in (prototype.initializer_list);
      ast.expression.results.failure.if {
        add_error (ast.expression.results.err);
      }.elseif {ast.expression.results.is_empty} then {
        add_error (ast.position.error("Missing initial value"));
      } else {
        (!ast.expression.results.is_single).if {
          add_error (ast.position.error("Dropping the last " + (ast.expression.results.count - 1).to_string + " values"));
        }.elseif {shared} then {
          r := shared_data_variable;
        } else {
          r := INSTR_GETELEMPTR
            .clone
            .make (prototype.initializer_list.self) slot Self
            .add_in (prototype.initializer_list)
            .result;
        };
        INSTR_STORE
          .clone
          .make_store_in r
            value (ast.expression.results.first)
          .add_in (prototype.initializer_list);
      };
    };
  );

  - compile_slot <-
  [ -? {! external}; ]
  (
    (internal).if {
      compile_opcode_in list;
    }.elseif {slot_data} then {
      // Read data slot
      shared.if {
        list.add_result (INSTR_LOAD
          .clone
          .make shared_data_variable
          .add_in list
          .result);
      } else {
        list.add_result (INSTR_LOAD
          .clone
          .make_slot Self receiver (list.self) list list
          .add_in list
          .result);
      };
    }.elseif {ast.expression != NULL} then {
      // Nominal case
      
      + exprs :EXPRS;
      exprs := ast.expression.compile_in list.results;
      
      exprs.failure.if {
        add_error(exprs.err.extend(ast.position, "Dropping return value #1 for slot " + name));
      }.elseif {! exprs.is_empty} then {
        exprs.is_single.if_false {
          add_error(ast.position.error("Only keep the first return value"));
        };
        (exprs.first.type = result_type).if {
          list.add_result (exprs.first.reg);
        }.elseif {has_result} then {
          add_error(ast.expression.position.error(
            "Incompatible type for return value #1. "
            + "Got: " + pretty_type(exprs.first.type)
            + ", expected: " + pretty_type(result_type)));
        };
      };
    } else {
      // Empty slot
    };
    ((has_result) && {list.results.is_empty}).if {
      add_error(ast.position.error(
        "No value given for this slot. Using prototype "
        + pretty_type(result_type)));
      list.add_result (result_type.prototype.compile_prototype_value_in list);
    };
    export.if {
      prototype_self := prototype.compile_prototype_value_in list;
    };
  );
  
  - compile_opcode_in list:LIST <-
  [
    -? { internal_opcode != NULL };
  ]
  (
    (internal_opcode == "object_size").if {
      list.add_result (INSTR_SIZEOF
        .clone
        .make (self.type.prototype.base_type)
        .add_in list
        .result);
    }.elseif {internal_opcode == "value"} then {
      INSTR_CALL
        .clone
        .make_ptrcall (list.self)
        .finalize_in list
        .if_fail { e:ERR;
          add_error(e.extend(ast.position, "Calling the block"));
        };
    } else {
      add_error(ast.position.error("Unknown opcode " + internal_opcode));
    }
  );

Section LIST_PROC

  - link gen:GENERATOR <-
  (
    external.if_false {
      ((slot_data) && {shared}).if {
        gen.alloc_global shared_data_variable;
      };
      list.link gen;
    };
  );
  
  - generate gen:GENERATOR <-
  (
    external.if {
      gen.declare_sub external_name args {
        arguments.iterate.loop_v { v:REG;
          gen.arg { v.base_type.generate gen; };
        };
      } return {
        (result_type = NULL).if {
          gen.type_void;
        } else {
          result_type.base_type.generate gen;
        };
      };
    } else {
      ((slot_data) && {shared}).if {
        gen.global_variable shared_data_variable;
      };
      generate_slot gen;
    };
  );

Section Private

  - generate_slot gen:GENERATOR <-
  [
    -? {! external};
    -? { ! self.reg.is_realized };
  ]
  (
    list.assert_result_coherent_with(result_type);

    gen.sub external_name self self args {
      export.if_false {
        self.reg.generate_arg gen;
      };
      arguments.iterate.loop_v { v:REG;
        v.generate_arg gen;
      };
    } do {
      export.if {
        ? { prototype_self != NULL };
        self.reg.realize_using (prototype_self);
      };
      ? { self.reg.is_realized };
      list.generate gen;
    } return (list.first_result_or_invalid);
  );

Section PROTOTYPE

  - set_type_index i:INTEGER <-
  [ -? {i >= 0}; ]
  (
    type_index := i;
  );

Section ITM_EXPR_WRITE

  + shared_data_variable :REG;

Section Private

  + ast :ITM_SLOT;
  
  + prototype_self :EXPR;


