Section Header

  + name := SLOT;

Section Inherit

  + parent_proc :Expanded PROC;

Section Public

  - name :CSTRING <- ast.identifier;
    
  + list :LIST;
  
  - external :BOOLEAN <- ast.is_external;
  
  + external_name :CSTRING <- name;
  
  + arguments :FAST_ARRAY(VAR);
  
  + prototype :PROTOTYPE;

  + self :VAR;

Section ITM_SLOT

  - make s:ITM_SLOT :SELF <-
  ( + t:TYPE;
    arguments := FAST_ARRAY(VAR).create 0;
    list := LIST.clone.make Self;
    ast  := s;
    prototype := ast.prototype_parent;
    ? {prototype != NULL};
    self := VAR.clone.make INSTR_NOP static_type (t := prototype.to_type);
    ? { self.is_valid };
    ? { t != NULL };
    ? { self.type != NULL };
    ? { ! self.is_realized };
    prototype.add_slot Self;
    Self
  );
  
  - add_argument v:VAR <- arguments.add_last v;

Section LIST_PROC

  - compile list_procs:LIST_PROC <-
  [
   ? { self.is_valid };
  ]
  (
    list.set_list_procs list_procs;
    external.if {
      + sym :ITM_EXPR_SYM;
      sym ?= ast.expression;
      ? { sym != NULL };
      external_name := sym.symbol;
    }.elseif {ast.expression != NULL} then {
      ast.expression.compile_in list;
    };
  );
  
  - link <-
  (
    external.if_false {
      list.link;
    };
  );
  
  - generate gen:GENERATOR <-
  (
    warnln ("generate "+name);
    external.if {
      gen.declare_sub external_name args {
        arguments.iterate.loop_v { v:VAR;
          gen.arg { v.type.generate gen; };
        };
      };
    } else {
      ? { ! self.is_realized };
      gen.sub external_name self self args {
        arguments.iterate.loop_v { v:VAR;
          v.generate_arg gen;
        };
      } do {
        is_block_value_slot.if {
          gen.call_ptr (list.self);
        } else {
          list.generate gen;
        };
      };
    };
  );

Section Private

  - is_block_value_slot :BOOLEAN <-
  ( (prototype.role == "Block") &&
    {ast.has_keyword "Internal"} &&
    {ast.has_keyword "Value"}
  );

  + ast :ITM_SLOT;


