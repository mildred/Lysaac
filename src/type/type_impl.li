Section Header

  + name := TYPE_IMPL;
  
  - export := LOW_LEVEL_TYPE;

Section Inherit

  + parent_type :Expanded TYPE;
  
Section Public
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Identification
  //
  
  - name :CSTRING <- (keywords_str + prototype.name).to_abstract_string;
  - is_void :BOOLEAN <- base_type.is_void;

  + prototype :PROTOTYPE;
  
  + base_type :LOW_LEVEL_TYPE <-
  [ -? { prototype.defined }; ]
  (
    block.if {
      base_type := LOW_LEVEL_TYPE.create_pointer (LOW_LEVEL_TYPE.create_function);
    } else {
      expanded.if {
        base_type := prototype.base_type;
      } else {
        base_type := LOW_LEVEL_TYPE.create_pointer (prototype.base_type);
      };
    };
    base_type
  );
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Create Register
  //

  - can_create_reg :BOOLEAN <- prototype.defined;
  
  - create_reg :REG <- REG.clone.make_type base_type;
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Generate
  //

  - generate gen:GENERATOR <-
  (
    (base_type = NULL).if {
      crash_with_message ("Cannot generate type for "+prototype.name);
      not_yet_implemented;
    } else {
      base_type.generate gen;
    };
  );
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Implementation Slots
  //
  
  - consistent_with_base t:LOW_LEVEL_TYPE :BOOLEAN <- ( base_type == t );

  - to_impl :TYPE_IMPL <- Self;
  
  - is_impl :BOOLEAN <- TRUE;
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Other
  //
  
  - to_string :STRING <-
    type_name + "<" + name + " " + base_type.to_string + ">";

  - to_low_level_type :LOW_LEVEL_TYPE <- base_type;

  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Creation
  //

Section PROTOTYPE

  - make_reference p:PROTOTYPE :SELF <-
  [
    -? { p.made };
    -? { p.role !== "Block" };
  ]
  (
    block := FALSE;
    expanded := FALSE;
    prototype := p;
    Self
  );

  - make_expanded p:PROTOTYPE :SELF <-
  [
    -? { p.made };
    -? { p.role !== "Block" };
  ]
  (
    block := FALSE;
    expanded := TRUE;
    prototype := p;
    Self
  );
  
  - make_block p:PROTOTYPE :SELF <-
  [
    -? { p.made };
    -? { p.role == "Block" };
  ]
  (
    block := TRUE;
    prototype := p;
    expanded := FALSE;
    Self
  );

