Section Header

  + name := TYPE;

Section Inherit

  + parent_any :Expanded ANY;
  
Section Public
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Identification
  //
  
  - name      :CSTRING <- deferred;
  + block     :BOOLEAN := FALSE;
  - reference :BOOLEAN <- ( ! expanded );
  + expanded  :BOOLEAN := FALSE;
  + strict    :BOOLEAN := FALSE;
  - void      :BOOLEAN <- TRUE;
  
  - ref_base_type :BASE_TYPE <- (deferred; BASE_TYPE);
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Create Register
  //

  - can_create_reg :BOOLEAN <- FALSE;
  
  - create_reg :REG <- deferred;
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Generate
  //

  - generate gen:GENERATOR <-
  (
    crash "Try to generate type " + name
  );
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Implementation Slots
  //
  
  - consistent_with_base t:BASE_TYPE :BOOLEAN <- deferred;
  
  - to_impl_of other:TYPE_IMPL :MAYBE(TYPE_IMPL) <-
  ( + res:MAYBE(TYPE_IMPL);
    (other = NULL).if {
      void.if {
        res << to_impl;
      } else {
        res <! ERR.create(("Void is not consistent with "+name).to_abstract_string);
      };
    }.elseif {consistent_with_base other} then {
      res << other;
    } else {
      res <! ERR.create(("Type "+other.name+" is not consistent with "+name).to_abstract_string);
    };
    res
  );

  + to_impl :TYPE_IMPL;
  
  - is_impl :BOOLEAN <- FALSE;
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Creation
  //
  
  - make_with_implementation impl:TYPE_IMPL :SELF <-
  (
    to_impl := impl;
    Self
  );
  
  //////////////////////////////////////////////////////////////////////////////
  //
  // Other
  //
  
  - to_string :STRING <- type_name + "<" + name + ">";

Section TYPE
  
  - keywords_str spc:CHARACTER :CSTRING <-
  ( + res :STRING;
    res := "".to_string;
    reference.if {
      res.append "Reference";
      res.add_last spc;
    } else {
      res.append "Expanded";
      res.add_last spc;
    };
    strict.if {
      res.append "Strict";
      res.add_last spc;
    };
    res
  );
