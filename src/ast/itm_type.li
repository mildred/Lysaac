Section Header

  + name := ITM_TYPE;

Section Inherit

  + parent_itm :Expanded ITM;

Section Public

  + prototype :ITM_EXPR_PROTO;
  
  + keywords :ITM_KEYWORDS;
  
  + block :BOOLEAN;
  
  - to_type :MAYBE(TYPE_IMPL) <-
  ( + res :MAYBE(TYPE_IMPL);
  
    warnln "FIXME(ITM_TYPE): use keywords";
  
    block.if {
      cluster_parent.prototype_for_role "Block".if { p:PROTOTYPE;
        res << p.to_type_block;
      } else_error { e:ERR;
        res <! position.error("Could not find prototype for role Block");
      };
    } else {
      slot_parent.find_prototype (prototype.prototype).if { p:PROTOTYPE;
        + t :TYPE_IMPL;
        ? { p.made };
        (keywords = NULL).if_false {
          keywords.has "Expanded".if {
            t := p.to_type_expanded;
          }.elseif {keywords.has "Reference"} then {
            t := p.to_type_reference;
          };
        };
        (t = NULL).if {
          t := p.to_type;
        };
        res << t;
      } else_error { e:ERR;
        res <! position.error("Could not find type " + name);
      };
    };
    res
  );
  
  - name :CSTRING <- prototype.prototype;

Section PARSER
  
  - make_block :SELF <-
  (
    prototype := NULL;
    keywords  := NULL;
    block     := TRUE;
    Self
  );
  
  - make (k:ITM_KEYWORDS, p:ITM_EXPR_PROTO) :SELF <-
  (
    prototype := p;
    keywords  := k;
    block     := FALSE;
    Self
  );

