Section Header

  + name := INSTR_CALL;

Section Inherit

  + parent_instr :Expanded INSTR;

Section Public

  //
  // Receiver
  //

  + receiver :VAR;
  
  //
  // Slot
  //

  - slot :SLOT <-
  ( + res :SLOT;
    (res = NULL).if {
      res := receiver.type.prototype.find_slot name;
    };
    res
  );

  //
  // Link
  //
  
  - link <-
  (
    (slot = NULL).if {
      crash_with_message ("Cannot find slot "+name+" in "+receiver.type.prototype.name);
    };
  );

  //
  // Generate
  //

  - generate gen:GENERATOR <-
  (
    gen.call (slot.name) args {
      args.iterate.loop_v { a:VAR;
        gen.call_arg a type { a.type.generate gen };
      };
    };
  );

Section ITM_EXPR_READ

  - make_itm i:ITM_EXPR_READ receiver r:VAR :SELF <-
  [ -? { r.type != NULL }; ]
  (
    receiver := r;
    ast      := i;
    args     := FAST_ARRAY(VAR).create 0;
    Self
  );
  
  - add_argument v:VAR <-
  (
    args.add_last v;
  );

Section Private

  - name :CSTRING <- ast.slot;
  
  + ast  :ITM_EXPR_READ;
  + args :FAST_ARRAY(VAR);

