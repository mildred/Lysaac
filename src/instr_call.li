Section Header

  + name := INSTR_CALL;

Section Inherit

  + parent_instr :Expanded INSTR;

Section Public

  //
  // Receiver
  //

  + receiver :REG;
  
  //
  // Slot
  //

  + slot :SLOT <-
  (
    slot := receiver.type.prototype.find_slot name
  );

  //
  // Link
  //
  
  - link <-
  (
    (slot = NULL).if {
      crash_with_message ("Cannot find slot "+name+" in "+receiver.type.prototype.name);
    };
  );

  //
  // Generate
  //

  - generate gen:GENERATOR <-
  (
    gen.call (slot.name) args {
      generate_arg receiver in gen;
      args.iterate.loop_v { a:REG;
        generate_arg a in gen;
      };
    } result result;
  );

Section Private

  - generate_arg arg:REG in gen:GENERATOR <-
  [ -? { arg.type != NULL }; ]
  (
    arg.type.base_type.void.if_false {
      gen.call_arg arg type { arg.type.generate gen };
    };
  );

Section ITM_EXPR_READ

  - make_itm i:ITM_EXPR_READ receiver r:REG :SELF <-
  [ -? { r.type != NULL }; ]
  (
    receiver := r;
    ast      := i;
    args     := FAST_ARRAY(REG).create 0;
    result   := REG.clone;
    (slot.result_type != NULL).if {
      result.make Self static_type (slot.result_type);
    };
    Self
  );
  
  - add_argument v:REG <-
  [
    -? { v.type != NULL };
  ]
  (
    args.add_last v;
  );

Section Private

  - name :CSTRING <- ast.slot;
  
  + ast  :ITM_EXPR_READ;
  + args :FAST_ARRAY(REG);

