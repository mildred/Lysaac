Section Header

  + name := INSTR_CALL;

Section Inherit

  + parent_instr :Expanded INSTR;

Section Public

  //
  // Receiver
  //

  + receiver :EXPR;  
  
  //
  // Slot
  //

  + slot :SLOT <-
  (
    slot := receiver.type.prototype.find_slot name
  );

  //
  // Link
  //
  
  - link <-
  (
    (slot = NULL).if {
      crash_with_message ("Cannot find slot "+name+" in "+receiver.type.prototype.name);
    };
  );

  //
  // Generate
  //

  - generate gen:GENERATOR <-
  (
    gen.call (slot.name) args {
      slot.external.if_false {
        generate_arg receiver in gen;
      };
      args.iterate.loop_v { a:EXPR;
        generate_arg a in gen;
      };
    } result result;
  );

Section Private

  - generate_arg arg:REG in gen:GENERATOR <-
  [ -? { arg.is_valid }; ]
  (
    arg.base_type.void.if_false {
      gen.call_arg arg type { arg.base_type.generate gen };
    };
  );

Section ITM_EXPR_READ

  - make_itm i:ITM_EXPR_READ receiver e:EXPR :SELF <-
  [ -? { e.is_valid }; ]
  (
    receiver      := e;
    ast           := i;
    args          := FAST_ARRAY(EXPR).create 0;
    result        := REG.clone;
    (slot.result_type != NULL).if {
      result.make_type (slot.result_type);
    };
    Self
  );
  
  - add_argument v:EXPR <-
  [
    -? { v != NULL };
  ]
  (
    args.add_last v;
  );

Section Private

  - name :CSTRING <- ast.slot;
  
  + ast  :ITM_EXPR_READ;
  + args :FAST_ARRAY(EXPR);

