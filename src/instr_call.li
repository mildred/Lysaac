Section Header

  + name := INSTR_CALL;

Section Inherit

  + parent_instr :Expanded INSTR;

Section Public

  //
  // Receiver
  //

  + receiver :EXPR;  
  
  //
  // Slot
  //

  + slot :SLOT;

  //
  // Link
  //
  
  - link;

  //
  // Generate
  //

  - generate gen:GENERATOR <-
  (
    gen.call (slot.name) args {
      slot.external.if_false {
        generate_arg receiver in gen;
      };
      args.iterate.loop_v { a:EXPR;
        generate_arg a in gen;
      };
    } result result;
  );

Section Private

  - generate_arg arg:REG in gen:GENERATOR <-
  [ -? { arg.is_valid }; ]
  (
    arg.base_type.void.if_false {
      gen.call_arg arg type { arg.base_type.generate gen };
    };
  );

Section ITM_EXPR_READ

  - make_read n:CSTRING receiver e:EXPR slot s:SLOT :SELF <-
  [
    -? { s != NULL  };
    -? { e.is_valid };
  ]
  (
    receiver      := e;
    name          := n;
    args          := FAST_ARRAY(EXPR).create 0;
    result        := REG.clone;
    slot          := s;
    (slot.result_type != NULL).if {
      result.make_type (slot.result_type);
    };
    Self
  );
  
  - add_argument v:EXPR <-
  [
    -? { v != NULL };
  ]
  (
    args.add_last v;
  );

Section Private

  + name :CSTRING;
  
  + args :FAST_ARRAY(EXPR);

