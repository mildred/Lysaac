Section Header

  + name := INSTR_STORE; // store slot

Section Inherit

  + parent_instr :Expanded INSTR;

Section Public

  //
  // Link
  //
  
  - link <-
  (
    ? {slot.is_compiled_data_slot};
  );

  //
  // Generate
  //

  - generate gen:GENERATOR <-
  ( + r:REG;
    r := REG.clone.make_type (BASE_TYPE.create_pointer (slot.result_type));
    gen.getelementptr_of receiver in r refs {
      gen.arg_i32 0;
      gen.arg_i32 (slot.type_index);
    };
    gen.store value to r;
  );

Section ITM_EXPR_WRITE

  - make_itm itm:ITM_EXPR_WRITE store v:REG in e:EXPR :SELF <-
  [
    -? { v.is_valid };
    -? { e.is_valid };
  ]
  (
    slot       := e.type.prototype.find_slot (itm.slot_name);
    receiver   := e;
    value      := v;
    Self
  );

Section SLOT

  - make_slot s:SLOT receiver r:REG value v:REG :SELF <-
  (
    receiver := r;
    slot     := s;
    value    := v;
    Self
  );

Section Private

  + receiver   :REG;
  + value      :REG;
  + slot       :SLOT;

