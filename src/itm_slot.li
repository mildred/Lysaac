Section Header

  + name := ITM_SLOT;

Section Inherit

  + parent_itm :Expanded ITM;
  
  - parent_i_find_type :Expanded I_FIND_TYPE;

Section Public

  - find_type type_name:CSTRING :TYPE <- prototype_parent.find_type type_name;
  
  - slot_parent :ITM_SLOT <- Self;

  + section :ITM_SECTION;

  + style :ITM_STYLE;
  
  + identifier :CSTRING;
  
  + expression :ITM_EXPR;
  
  - is_external :BOOLEAN <- ( has_keyword "External" );
  
  - has_keyword k:CSTRING :BOOLEAN <-
  (
     + res :BOOLEAN;
    (keywords != NULL).if {
      res := keywords.has k;
    };
    res
  );
  
  + arguments :FAST_ARRAY(ITM_EXPR_ARG);

Section ITM

  - lookup_local name:CSTRING :ITM_EXPR_LOCAL <-
  ( + it :ITERATOR(ITM_EXPR_ARG);
    + res :ITM_EXPR_LOCAL;
    
    (it := arguments.iterate).loop_v { l:ITM_EXPR_ARG;
      (l.name = name).if {
        it.stop;
        res := l;
      };
    };
    (res = NULL).if {
      res := itm_parent.lookup_local name;
    };
    res
  );

Section ITM_PROTOTYPE

  - compile_in list:LIST_SLOT <-
  ( + slot:SLOT;
  
    ? {prototype_parent != NULL};
  
    slot := SLOT.clone.make Self;
    arguments.iterate.loop_v { l:ITM_EXPR_ARG;
      slot.add_argument (l.get_argument_variable);
    };
    list.add_slot slot;
  );
  
Section PARSER

  - make :SELF <-
  (
    arguments := FAST_ARRAY(ITM_EXPR_ARG).create 0;
    Self
  );
  
  - set_section s:ITM_SECTION :SELF <-
  (
    section := s;
    Self
  );
  
  - set_style s:ITM_STYLE :SELF <-
  (
    style := s;
    Self
  );
  
  - add_identifier idf:CSTRING :SELF <-
  [ -? {identifier = NULL}; ]
  (
    identifier := idf;
    Self
  );
  
  - add_argument (idf:CSTRING, type:ITM_TYPE) :SELF <-
  (
    arguments.add_last (ITM_EXPR_ARG.clone.make (idf, type).set_itm_parent Self);
    Self
  );
  
  - set_keywords k:ITM_KEYWORDS :SELF <-
  (
    keywords := k;
    Self
  );
  
  - set_expression e:ITM_EXPR :SELF <-
  (
    e.set_itm_parent Self;
    expression := e;
    Self
  );
  
Section Private
  
  + keywords :ITM_KEYWORDS;

