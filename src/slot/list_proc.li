Section Header

  + name := LIST_PROC;

Section Inherit

  + parent_any :Expanded ANY;

Section Public

  - compile <-
  (
    slots.iterate.loop_v { s:PROC;
      progress_step (cur_step,num_steps) message ("Compile " + s.external_name);
      cur_step := cur_step + 1;
      s.compile Self;
    };
  );
  
  - link gen:GENERATOR <-
  (
    constants.iterate.loop_v { i:INSTR_STRING;
      i.link_constant gen;
    };
    slots.iterate.loop_v { s:PROC;
      progress_step (cur_step,num_steps) message ("Link " + s.external_name);
      cur_step := cur_step + 1;
      s.link gen;
    };
  );

  - generate gen:GENERATOR <-
  (
    constants.iterate.loop_v { i:INSTR_STRING;
      i.generate_constant gen;
    };
    slots.iterate.loop_v { s:PROC;
      progress_step (cur_step,num_steps) message ("Generate " + s.external_name);
      cur_step := cur_step + 1;
      s.generate gen;
    };
  );
  
  - count :INTEGER <- slots.count;
  
  - warn_errors <-
  (
    slots.iterate.loop_v { s:PROC;
      s.warn_errors;
    };
  );
  
Section CLUSTER

  - make :SELF <-
  (
    slots := FAST_ARRAY(PROC).create 0;
    constants := FAST_ARRAY(INSTR_STRING).create 0;
    Self
  );
  
  - set_percentage (num, cur :INTEGER) <-
  (
    num_steps := num;
    cur_step  := cur;
  );
  
  + num_steps :INTEGER;
  + cur_step  :INTEGER;

Section PROTOTYPE, SLOT

  - remove_slot s:SLOT <-
  ( + i:INTEGER;
    i := slots.fast_first_index_of s;
    slots.valid_index i.if {
      slots.remove i;
    };
  );

Section ITM_SLOT, ITM_EXPR_GROUP, SLOT, PROTOTYPE

  - add_slot s:SLOT <-
  (
    //warnln("add slot: "+s.external_name);
    slots.add_last s;
  );
  
  - add_block b:PROC_BLOCK <-
  (
    slots.add_last b;
  );

Section INSTR_STRING

  - add_constant cst:INSTR_STRING <-
  (
    constants.fast_has cst.if_false {
      constants.add_last cst;
    };
  );

Section Private

  + constants :FAST_ARRAY(INSTR_STRING);

  + slots :FAST_ARRAY(PROC);

