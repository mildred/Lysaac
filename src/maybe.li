Section Header

  + name := Expanded MAYBE(E);
  
  - import := E;
  //- export := E;

Section Public

  + has_element :BOOLEAN := FALSE;
  
  - ok :BOOLEAN <- has_element;
  - ko :BOOLEAN <- ( ! ok );

  + element :E;
  
  - elem :E <- element;
  
  - make e:E :SELF <-
  (
    element     := e;
    has_element := TRUE;
    Self
  );
  
  - clear <-
  ( + e:E;
    element     := e;
    has_element := FALSE;
  );
  
  - to_e :E <-
  [ -? { has_element }; ]
  ( element );
  
  - from_e e:E :SELF <-
  ( make e )
  [ +? { has_element }; ];

  - if true_blc:{E;} :BOOLEAN <-
  ( has_element.if { true_blc.value element; }; has_element );

  - if true_blc:{E;} else false_blc:{} :BOOLEAN <-
  ( has_element.if { true_blc.value element; } else false_blc; has_element );
