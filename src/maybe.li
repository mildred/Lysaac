Section Header

  + name := Expanded MAYBE(E);
  
  - import := E;
  //- export := E;

Section Public

  + has_element :BOOLEAN := FALSE;
  
  - ok :BOOLEAN <- has_element;
  - ko :BOOLEAN <- ( ! ok );

  + element :E;
  
  + error :ERR;
  
  - elem :E <- element;
  
  - make e:E :SELF <-
  (
    element     := e;
    has_element := TRUE;
    Self
  );
  
  - make_error e:ERR :SELF <-
  (
    clear;
    error := e;
    Self
  );
  
  - clear <-
  ( + e:E;
    element     := e;
    error       := ERR;
    has_element := FALSE;
  );

  - clone :SELF <-
  // Very ugly hack !!!
  ( + e:SELF;
    + r:REFERENCE(SELF);
    r := REFERENCE(SELF).create SELF;
    e := r.to_e;
    e
  );
  
  - to_e :E <-
  [ -? { has_element }; ]
  ( element );
  
  - from_e e:E :SELF <-
  ( make e )
  [ +? { has_element }; ];
  
  - if_true true_blc:{E;} :SELF <-
  ( has_element.if { true_blc.value element; }; Self );

  - if_false false_blc:{ERR;} :SELF <-
  ( has_element.if_false { false_blc.value (error); }; Self );

  - if_false2 false_blc:{REFERENCE(ERR);} :SELF <-
  ( has_element.if_false { false_blc.value (REFERENCE(ERR).create error); }; Self );

  - if true_blc:{E;} :BOOLEAN <-
  ( has_element.if { true_blc.value element; }; has_element );

  - if true_blc:{E;} else false_blc:{} :BOOLEAN <-
  ( has_element.if { true_blc.value element; } else false_blc; has_element );
