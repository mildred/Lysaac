Section Header

  + name      := PARSER_CLI;

Section Inherit

  + parent_parser :Expanded PARSER;

Section SELF

  + cluster :CLUSTER;

  - read_cli <-
  (
    read_section_header;
  );
  
  - hook_h_slot (ident:CSTRING, style:ITM_STYLE, expr:ITM_EXPR) <-
  (
    (ident == "name").if {
      expr.is_a ITM_EXPR_PROTO.if { + p:ITM_EXPR_PROTO;
        p ?= expr;
        (p.prototype.as_lower == cluster.name).if_false {
          print_warning;
          "expected ".print; cluster.name.as_upper.println;
        };
      } else {
        print_warning;
        "expected prototype constant".println;
      };
    }.elseif {ident == "path"} then {
      expr.is_a ITM_EXPR_STR.if { + s:ITM_EXPR_STR;
        s ?= expr;
        cluster.set_path (s.string);
      } else {
        print_warning;
        "expected string constant".println;
      };
    } else {
      print_warning;
      "unknown slot ".print; ident.println;
    };
  );

Section CLUSTER

  - make c:CLUSTER :SELF <-
  (
    cluster := c;
    Self
  );
  
  - parse <-
  (
    open_source (cluster.filename);
    read_cli;
  );
