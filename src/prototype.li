Section Header

  + name := PROTOTYPE;

Section Inherit

  + parent_any :Expanded ANY;
  
  - parent_type_finder :Expanded TYPE_FINDER;
  
  + parent_error_collector :Expanded ERR_COLLECTOR;

Section Public

  - made      :BOOLEAN <- state >= state_made;      // Object is made and ready
  - parsed    :BOOLEAN <- state >= state_parsed;    // AST is parsed
  - defining  :BOOLEAN <- state == state_defining;  // External interfaces are being defined
  - defined   :BOOLEAN <- state >= state_defined;   // External interfaces are defined
  - compiled  :BOOLEAN <- state >= state_compiled;  // Internal code is compiled
  - generated :BOOLEAN <- state >= state_generated; // Code is generated.

  - find_type type_name:CSTRING :MAYBE(TYPE) <-
  [
    -? { made };
    -? { cluster.parsed };
  ]
  (
    cluster.find_type type_name
  );
  
  + base_type :BASE_TYPE <-
  // The Expanded LLVM type
  [ -? {defined}; ]
  (
    (role == "Block").if {
      base_type := BASE_TYPE.create_function;
    } else {
      base_type := BASE_TYPE.create_struct;
      slots.iterate.loop_v { s:SLOT;
        s.shared.if_false {
          base_type.struct_types.add_last (s.result_type.to_impl.base_type);
        };
      };
    };
    base_type
  );
  
  + is_polymorphic :BOOLEAN;
  
  + has_dispatch_value :BOOLEAN;
  
  + dispatch_value :INTEGER;
  
  - void :BOOLEAN <- base_type.void;
  
  + filename :CSTRING;
  
  + role :CSTRING := "";
  
  + cluster :CLUSTER;
  
  + expanded :BOOLEAN;
  
  + name :CSTRING <-
  ( + str :ABSTRACT_STRING;
    
    str := PATH_HELPER.basename filename;
    name := str.substring (str.lower) to (str.upper - 3).as_upper
  );
  
  + position :POS;
  
  - parse <-
  [ -? {made}; -? {! parsed}; ]
  ( + e :ERR;
    (ast, e) := parser.parse;
    e.failure.if { add_error e; };
    state := state_parsed;
  )
  [ +? {parsed}; ];
  
  - define_in list:LIST_PROC <-
  [ -? {parsed}; -?{!defined}; ]
  (
    state := state_defining;
    ast.define_in list;
    check_slot_name_unicity list;
    is_polymorphic.if {
      + s :SLOT_PLYM;
      s := SLOT_PLYM.clone.make_in Self;
      add_slot s;
      list.add_slot s;
      //warnln("Add slot plym " + s.to_string);
    };
    state := state_defined;
  )
  [ +? { defined }; ];
  
  - compile <-
  [ -? {defined}; -? {! compiled}; ]
  (
    prototype_rvalue := REG.clone.make_type (BASE_TYPE.create_pointer base_type);
    initializer_list := LIST
      .clone
      .make_error_collector Self type NULL;
    initializer_list.make_self (prototype_rvalue_expr);
    state := state_compiled;
  )
  [ +? {compiled}; ];
  
  - link gen:GENERATOR <-
  (
    global_ctor_sub := REG.clone;
    gen.alloc_global global_ctor_sub;
    initializer_list.link gen;
    state := state_linked;
  );
  
  - generate gen:GENERATOR <-
  [ -? {compiled}; -? {! generated}; ]
  (
    generate_prototype_value gen;
    generate_initializer gen;
    state := state_generated;
  )
  [ +? {generated}; ];

  - to_string :STRING <- type_name + "<" + name + ">";
  
  - compile_prototype_value_in l:LIST :EXPR <-
  [ -? { l != NULL }; ]
  ( + res:REG;
    
    expanded.if {
      res := INSTR_LOAD
        .clone
        .make prototype_rvalue
        .add_in l
        .result;
    } else {
      res := prototype_rvalue;
    };
    
    EXPR.clone.make (res, to_type)
  );

Section Private

  - check_slot_name_unicity list:LIST_PROC <-
  ( + it:ITERATOR(SLOT);
    
    (it := slots.iterate_reverse).loop_v { dup:SLOT;
      find_slot (dup.name).if { s:SLOT;
        (dup != s).if {
          add_error (s.position.error ("Slot " + s.name + " has a duplicate:"));
          add_error (dup.position.error ("Discarding this slot"));
          slots.remove (it.index);
          list.remove_slot dup;
        };
      };
    };
  );

  - generate_initializer gen:GENERATOR <-
  (
    gen.ctor_sub 65535 in global_ctor_sub do {
      ? { prototype_rvalue.is_realized };
      initializer_list.generate gen;
    };
  );

  - generate_prototype_value gen:GENERATOR <-
  [ -? { ! prototype_rvalue.is_realized }; ]
  (
    (role == "Block").if {
      gen.global_block_prototype_value prototype_rvalue name name;
    } else {
      gen.global_prototype_value prototype_rvalue name name;
    };
  )
  [ +? { prototype_rvalue.is_realized }; ];

Section ITM_EXPR

  - find_slot name:CSTRING :MAYBE(SLOT) <-
    find_slot name except NULL;

Section Private

  - find_slot name:CSTRING except slt:SLOT :MAYBE(SLOT) <-
  [ -? {parsed}; ]
  ( + it:ITERATOR(SLOT);
    + res :MAYBE(SLOT);
    
    (it := slots.iterate).loop_v { s:SLOT;
      ((s != slt) && {s.name = name}).if {
        res << s;
        it.stop;
      };
    };
    
    res.has_element.if_false {
      res <! position.error("Could not find slot " + name);
    };
    
    res
  );

Section CLUSTER, ITM_TYPE, SLOT, INSTR_FNPTR

  - to_type :TYPE_IMPL <-
  [ -? {made}; ]
  ( + type:TYPE_IMPL;

    (role == "Block").if {
      type := to_type_reference;
    }.elseif {expanded} then {
      type := to_type_expanded;
    } else {
      type := to_type_reference;
    };
    type
  );
  
  - to_type_expanded :TYPE_IMPL <-
  [
    -? {role !== "Block"};
    -? {made};
  ]
  (
    (type_exp = NULL).if {
      type_exp := TYPE_IMPL.clone.make_expanded Self;
    };
    type_exp
  );

  - to_type_reference :TYPE_IMPL <-
  [ -? {made}; ]
  (
    (type_ref = NULL).if {
      (role == "Block").if {
        type_ref := TYPE_IMPL.clone.make_block Self;
      } else {
        type_ref := TYPE_IMPL.clone.make_reference Self;
      };
    };
    type_ref
  );
  
  - to_type_block :TYPE_IMPL <-
  [ -? { role == "Block" }; ]
  ( to_type );

Section CLUSTER_ITEM

  - make_file file:CSTRING cluster c:CLUSTER :SELF <-
  (
    make_error_collector file;
    expanded := FALSE;
    cluster  := c;
    filename := file;
    ast      := NULL;
    slots    := FAST_ARRAY(SLOT).create 0;
    parser   := PARSER_LI.clone.make Self;
    position := POS.create(file, 1, 0);
    state    := state_made;
    initializer_list := NULL;
    prototype_rvalue := REG;
    type_ref   := NULL;
    type_exp   := NULL;
    num_compiled_data_slots := 0;
    is_polymorphic := FALSE;
    has_dispatch_value := FALSE;
    dispatch_value := 0;
    Self
  );

Section ITM_SLOT

  - set_dispatch_value i:INTEGER <-
  [ -? { ! has_dispatch_value }; ]
  (
    has_dispatch_value := TRUE;
    dispatch_value := i;
  );

Section PARSER_LI

  - set_role r:CSTRING <-
  [ -? {made}; -? {! parsed}; ]
  (
    role := r;
    cluster.set_prototype Self for_role r;
  );

  - set_base_type t:BASE_TYPE <-
  [
    -? {made};
    -? {! parsed};
    -? { t != NULL };
  ]
  (
    base_type := t;
  );
  
  - make_expanded :SELF <-
  (
    expanded := TRUE;
    Self
  );

Section SLOT

  + initializer_list :LIST; // TODO: generate

  - add_slot s:SLOT <-
  (
    s.inherit.if {
      ? {! is_polymorphic};
      is_polymorphic := TRUE;
    };
    add_error_collector_child s;
    slots.add_last s;
    ((s.style.plus) && {s.has_result}).if {
      s.set_type_index num_compiled_data_slots;
      num_compiled_data_slots := num_compiled_data_slots + 1;
    };
  );

Section SLOT_BLOCK

  - add_slot_block s:SLOT_BLOCK <-
  (
    add_error_collector_child s;
    slots.add_last s;
  );

Section ITM_SLOT

  + slots :FAST_ARRAY(SLOT);

Section Private

  + num_compiled_data_slots :INTEGER := 0;

  + state :INTEGER;
  
  - state_none      :INTEGER := 0;
  - state_made      :INTEGER := 1;
  - state_parsed    :INTEGER := 3;
  - state_defining  :INTEGER := 4;
  - state_defined   :INTEGER := 5;
  - state_compiled  :INTEGER := 6;
  - state_linked    :INTEGER := 7;
  - state_generated :INTEGER := 8;

  + parser :PARSER_LI;

  + ast :ITM_PROTOTYPE;
  
  + type_ref :TYPE_IMPL;
  
  + type_exp :TYPE_IMPL;
  
  + prototype_rvalue :REG; // Always a pointer value (reference)
  
  - prototype_rvalue_expr :EXPR <-
    EXPR.clone.make (prototype_rvalue, to_type_reference);
    
  + global_ctor_sub :REG;


