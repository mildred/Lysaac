///////////////////////////////////////////////////////////////////////////////
//                             Lisaac Library                                //
//                                                                           //
//                   LSIIT - ULP - CNRS - INRIA - FRANCE                     //
//                                                                           //
//   This program is free software: you can redistribute it and/or modify    //
//   it under the terms of the GNU General Public License as published by    //
//   the Free Software Foundation, either version 3 of the License, or       //
//   (at your option) any later version.                                     //
//                                                                           //
//   This program is distributed in the hope that it will be useful,         //
//   but WITHOUT ANY WARRANTY; without even the implied warranty of          //
//   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the           //
//   GNU General Public License for more details.                            //
//                                                                           //
//   You should have received a copy of the GNU General Public License       //
//   along with this program.  If not, see <http://www.gnu.org/licenses/>.   //
//                                                                           //
//                     http://isaacproject.u-strasbg.fr/                     //
///////////////////////////////////////////////////////////////////////////////
Section Header

  + name     := Strict MEMORY;


  - copyright   := "2003-2007 Benoit Sonntag";

  - comment  := "Memory manager 32/64 bits.";

  - external :=
`
#define MINIMUM_SIZE 512
void *table_size[MINIMUM_SIZE/sizeof(void *)];
void *last_block[64];
`;

Section Inherit

  - parent_object:OBJECT := OBJECT;

  //
  // MACRO ALLOCATOR
  //

Section Mapping

  + previous_linear:MEMORY;
  + size_and_id:UINTEGER_CPU;

  //---------------> Limit for Busy

  + next_free    :MEMORY;
  + previous_free:MEMORY;

  //---------------> Limit for Free

Section Private

  //
  // Product by compiler.
  //

  // For GC only.
  /*
  - get_object_size t:INTEGER :INTEGER <- `get_object_size(@t)`:INTEGER;

  - mark_static_object   <- `mark_static_object()`;

  - demark_static_object <- `demark_static_object()`;

  - mark_object ptr:POINTER type t:INTEGER <- `mark_object(@ptr,@t)`;

  - demark_object ptr:POINTER type t:INTEGER <- `demark_object(@ptr,@t)`;
  */

Section Public

  - object_size:INTEGER               <- POINTER.object_size + UINTEGER_CPU.object_size;

Section MEMORY

  - is_debug_memory:BOOLEAN := FALSE;

  - this:POINTER                      <- CONVERT(MEMORY,POINTER).on Self;

  - begin:POINTER                     <- this + object_size;

  - size:UINTEGER_CPU                 <- size_and_id & ~ 3.to_uinteger_cpu;

  - next_linear:MEMORY                <- CONVERT(POINTER,MEMORY).on (begin + size);

  - id:UINTEGER_CPU                   <- size_and_id & 01b;

  - id_end:UINTEGER_CPU               <- 10b;

  - is_end:BOOLEAN                    <- (size_and_id & id_end).to_boolean;

  - set_previous_linear p:MEMORY      <- ( previous_linear := p; );

  - set_size_and_id s:UINTEGER_CPU    <- ( size_and_id     := s; );

  - get_index p:POINTER :UINTEGER_CPU <- (p - begin_memory).to_uinteger_cpu >> 26;

  - nb_page:UINTEGER_CPU;

  - put_last m:MEMORY to idx:UINTEGER_CPU <-
  (
    ? {idx < nb_page};
    `last_block[@idx] = @m`;
  );

  - get_last idx:UINTEGER_CPU :MEMORY <-
  (
    ? {idx < nb_page};
    `last_block[@idx]`:MEMORY
  );

  - search_capacity <-
  (
    capacity_max := SYSTEM.get_memory_capacity;
    begin_memory := SYSTEM.get_begin_memory;
    /*
    "Capacity : ".print; capacity_max.print_hex; '\n'.print;
    "Begin    : ".print; begin_memory.print_hex; '\n'.print;
    */
    {begin_memory != NULL} ? "Memory: Not memory.";
    {(begin_memory & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";
  );

  - new_page:MEMORY <-
  ( + old_size,new_size:UINTEGER_CPU;
    + block:MEMORY;

    (capacity_max = 0).if {
      search_capacity;
    };
    old_size := nb_page << 26;
    nb_page  := nb_page + 1;
    new_size := old_size + 64.mb;
    (new_size > capacity_max).if {
      "Not enough memory.\n".print;
      die_with_code exit_failure_code;
    };
    /*
    "Begin_memory + old_size : ".print;
    (begin_memory + old_size).print_hex; '\n'.print;
    "object_size  ".print; object_size.print; '\n'.print;
    */
    block := CONVERT(POINTER,MEMORY).on (begin_memory + old_size);
    block.set_previous_linear NULL;
    block.set_size_and_id (id_free | id_end | (64.mb - object_size));
    block.add_link_free;
    put_last block to (nb_page - 1);

    block
  );

  //
  // Busy / Free Block.
  //

  - id_free:UINTEGER_CPU <- 00b;
  - id_busy:UINTEGER_CPU <- 01b;

  - set_next_free     n:MEMORY <- ( next_free     := n; );
  - set_previous_free p:MEMORY <- ( previous_free := p; );

  - delete_link_free <-
  ( + prev,next:MEMORY;

    prev := previous_free;
    next := next_free;
    (prev = NULL).if {
      first_free := next;
    } else {
      prev.set_next_free next;
    };
    (next != NULL).if {
      next.set_previous_free prev;
    };
  );

  - add_link_free <-
  (
    next_free     := first_free;
    previous_free := NULL;
    (first_free != NULL).if {
      first_free.set_previous_free Self;
    };
    first_free := Self;
  );

Section MEMORY

  - first_free:MEMORY;

  //
  // Management.
  //

  - to_free idx:UINTEGER_CPU <-
  ( + new_free,next:MEMORY;
    + new_size:UINTEGER_CPU;
    {id = id_busy} ? "Memory: Macro block not busy.";
    {idx.in_range 0 to 63} ? "Memory: Bound index.";

    next := next_linear;
    new_free := previous_linear;
    new_size := size_and_id & ~ 1.to_uinteger_cpu;
    ((new_free = NULL) || {new_free.id != id_free}).if {
      // `Self' => Free
      new_free := Self;
      add_link_free;
    } else {
      // `previous_linear' => Free
      new_size := new_size + new_free.size_and_id + object_size;
    };

    ((! is_end) && {next.id = id_free}).if {
      // Delete and concat `next_linear'
      new_size := new_size + next.size_and_id + object_size;
      next.delete_link_free;
    };
    new_free.set_size_and_id new_size;

    (new_free.is_end).if {
      put_last new_free to idx;
    } else {
      next := new_free.next_linear;
      next.set_previous_linear new_free;
    };
  );

  - to_busy sz:POINTER index idx:UINTEGER_CPU <-
  ( + siz,new_size:UINTEGER_CPU;
    + new,next:MEMORY;
    {id = id_free} ? "Memory: Macro block not free.";
    {(sz & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";

    delete_link_free;
    //
    siz      := size_and_id;
    new_size := siz - sz;
    (new_size > (minimum_size+object_size+object_size)).if {
      siz := sz;
      new := CONVERT(POINTER,MEMORY).on (begin+sz);
      new.set_previous_linear Self;
      new.set_size_and_id (new_size - object_size);
      new.add_link_free;
      (new.is_end).if {
	put_last new to idx;
      } else {
	next := new.next_linear;
	next.set_previous_linear new;
      };
    };
    size_and_id := siz | id_busy;
    {id = id_busy} ? "Memory: Macro Block not busy.";
  );

  - resize new_size:UINTEGER_CPU index idx:UINTEGER_CPU :MEMORY <-
  ( + nxt,result:MEMORY;
    + old_size,sz:UINTEGER_CPU;

    {(new_size & (POINTER.object_size -1)) = 0} ? "Memory: Alignment.";
    {idx.in_range 0 to 63} ? "Memory: Bound index.";

    old_size := size;
    (new_size > old_size).if {
      (! is_end).if {
	nxt := next_linear;
	sz  := new_size - old_size - object_size;
	((nxt.id = id_free) && {nxt.size >= sz}).if {
	  nxt.to_busy (sz.to_pointer) index idx;
	  size_and_id := size_and_id + (nxt.size_and_id & ~ 1.to_uinteger_cpu) + object_size;
	  (is_end).if {
	    put_last Self to idx;
	  } else {
	    nxt := next_linear;
	    nxt.set_previous_linear Self;
	  };
          result := Self;
        };
      };
      (result = NULL).if {
	// new allocation.
        result := search new_size;
        copy begin to (result.begin) size old_size;
        to_free idx;
      };
    } else {
      result := Self;
    };
    result
  );

  //
  // Searching.
  //

  - search new_size:UINTEGER_CPU :MEMORY <-
  ( + result:MEMORY;
    + idx:UINTEGER_CPU;


    {new_size > minimum_size-POINTER.object_size} ? "Memory: Big block.";
    {(new_size & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";

    result := first_free;
    {(result != NULL) && {result.size < new_size}}.while_do {
      result := result.next_free;
    };
    (result = NULL).if {
      result := new_page;
    };
    idx := get_index (result.this);
    {idx.in_range 0 to 63} ? "Memory: Bound index.";
    result.to_busy new_size index idx;

    result
  );

  - new_lab t:UINTEGER_32 :POINTER <-
  ( + idx:UINTEGER_CPU;
    + blc,prev:MEMORY;
    + result:POINTER;

    {
      (idx < nb_page) &&
      {
	blc := get_last idx;
	(blc.size < 4096) || {blc.id = id_busy}
      }
    }.while_do {
      idx := idx + 1;
    };

    (idx >= nb_page).if {
      blc := new_page;
    };
    blc.set_size_and_id (blc.size_and_id - 4096);
    result := blc.next_linear.this;

    (blc.size < minimum_size).if {
      blc.delete_link_free;
      prev := blc.previous_linear;
      (prev != NULL).if {
	prev.set_size_and_id (prev.size_and_id + blc.size_and_id + object_size);
	put_last prev to idx;
      };
    };
    put_32 t to result;

    {((result - begin_memory) & 0FFFh) = 0} ? "Memory: Alignment LAB.";
    result + UINTEGER_32.object_size
  );

Section Private

  - minimum_size:UINTEGER_CPU <- `MINIMUM_SIZE`:UINTEGER_CPU;

  - table_type idx:UINTEGER_32 :POINTER <-
  (
    //{idx.in_range 0 to 17} ? "Memory: Bound table_type.";
    `&(table_type[@idx])`:POINTER
  );

  - table_size idx:UINTEGER_CPU :POINTER <-
  (
    {idx.in_range 1 to (minimum_size/POINTER.object_size)} ?
    "Memory: Bound table_size.";
    `&(table_size[@idx-1])`:POINTER
  );

  - begin_memory:POINTER;

  - capacity_max:UINTEGER_CPU;

  - read_ptr p:POINTER :POINTER <-
  ( + mem:NATIVE_ARRAY(POINTER);
    mem := CONVERT(POINTER, NATIVE_ARRAY(POINTER)).on p;
    mem.item 0
  );

  - put_ptr v:POINTER to p:POINTER <-
  ( + mem:NATIVE_ARRAY(POINTER);
    mem := CONVERT(POINTER, NATIVE_ARRAY(POINTER)).on p;
    mem.put v to 0;
  );

  - put_32 v:UINTEGER_32 to p:POINTER <-
  ( + mem:NATIVE_ARRAY(UINTEGER_32);
    mem := CONVERT(POINTER, NATIVE_ARRAY(UINTEGER_32)).on p;
    mem.put v to 0;
  );

  - micro_alloc new_size:UINTEGER_CPU table ptr_table:POINTER lab lab_type:UINTEGER_32 :POINTER <-
  ( + result,next,next2:POINTER;
    + page:POINTER;
    {(new_size & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";
    {new_size >= POINTER.object_size} ? "Memory: Size = 0.";

    result := read_ptr ptr_table;
    (result = NULL).if {
      // Allocation new LAB.
      result := new_lab lab_type;
      next := result + new_size;
      put_ptr NULL to next;
      put_ptr next to ptr_table;
    } else {
      // Next Linked list.
      next := read_ptr result;
      (next = NULL).if {
	// Linear allocation.
	page := (result - begin_memory) & 0FFFh;
	((page + (new_size << 1)) <= 4096).if {
	  next := result + new_size;
	} else {
	  next := new_lab lab_type;
	};
	put_ptr NULL to next;
	put_ptr next to ptr_table;
      } else {
	// Linked list allocation.
	next2 := read_ptr next & ~ 11b.to_uinteger_cpu;
	put_ptr next2 to result;
	result := next;
      };
    };
    result
  );

  - micro_free p:POINTER table ptr_table:POINTER <-
  ( + next,last:POINTER;
    {p != NULL} ? "Memory: Pointer NULL.";

    last := read_ptr ptr_table;
    {last != NULL} ? "Memory: Table NULL.";
    next := read_ptr last;
    put_ptr (next | 11b) to p;
    put_ptr p to last;
  );

Section Private

  - copy src:POINTER to dst:POINTER size sz:UINTEGER_CPU <-
  ( + na_src,na_dst:NATIVE_ARRAY(POINTER);
    + siz:INTEGER;

    siz := sz.to_integer;
    {(siz & (POINTER.object_size -1)) = 0} ? "Memory: Copy alignment.";
    siz := siz / POINTER.object_size;
    na_src := CONVERT(POINTER, NATIVE_ARRAY(POINTER)).on src;
    na_dst := CONVERT(POINTER, NATIVE_ARRAY(POINTER)).on dst;
    //
    (siz - 1).downto 0 do { j:INTEGER;
      na_dst.put (na_src.item j) to j;
    };
  );

  - fill_memory src:POINTER size sz:UINTEGER_CPU <-
  // Just for debug.
  ( + na_src:NATIVE_ARRAY(POINTER);
    + siz:UINTEGER_CPU;

    {(sz & (POINTER.object_size -1)) = 0} ? "Memory: Copy alignment.";

    siz := sz.align_power (POINTER.object_size) / POINTER.object_size;
    na_src := CONVERT(POINTER,NATIVE_ARRAY(POINTER)).on src;
    ((siz-1).to_integer).downto 0 do { j:INTEGER;
      na_src.put NULL to j;
    };
  );
  /*
Section External

  - calloc (nb,sz:UINTEGER) :POINTER <-
  ( + result:POINTER;
    + size:UINTEGER_32;
    + tab:NATIVE_ARRAY(UINTEGER_8);

    size := nb*sz;
    "calloc = ".print;
    size.print; '\n'.print;
    result := malloc (size.to_uinteger);
    tab := CONVERT(POINTER,NATIVE_ARRAY(UINTEGER_8)).on result;
    0.to (size-1) do { i:INTEGER;
      tab.put 0 to i;
    };
    result
  );

  - malloc sz:UINTEGER :POINTER <-
  ( + result:POINTER;
    "malloc = ".print;
    sz.print; '\n'.print;
    result := MEMORY.alloc_dynamic (sz.to_pointer);
    (result = NULL).if {
      "MEMORY : MERDE\n".print;
    };
    "MEMORY :sortie\n".print;
    result
  );

  - free ptr:POINTER <-
  ( + mem:MEMORY;

    "FREE\n".print;
    (ptr != NULL).if {
      mem := CONVERT(POINTER, MEMORY).on (ptr - object_size);
      mem.to_free (get_index ptr);
      //MEMORY.free_dynamic ptr size 500;
    };
  );
  */
Section Public

  //
  // MICRO ALLOCATOR
  //

  - alloc_type t:UINTEGER_32 size sz:UINTEGER_CPU :POINTER <-
  // Allocation for object without type. (LAB_TYPE)
  ( + ptr_table,result:POINTER;
    + new_size:UINTEGER_CPU;

    verify_memory "Begin alloc_type size";

    {sz <= minimum_size} ? "Memory: Size bound.";

    new_size  := sz.align_power (POINTER.object_size);
    {(new_size & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";
    ptr_table := table_type t;
    result := micro_alloc new_size table ptr_table lab (t | 1b);

    verify_memory "End alloc_type size";

    result
  );

  - free p:POINTER type t:UINTEGER_32 <-
  ( + ptr_table:POINTER;
    {p != NULL} ? "Memory: Pointer NULL.";

    verify_memory "Begin free type";

    ptr_table := table_type t;
    micro_free p table ptr_table;

    verify_memory "End free type";
  );

  - alloc_size sz:UINTEGER_CPU :POINTER <-
  // Allocation for object with type. (LAB_SIZE)
  ( + ptr_table,result:POINTER;
    + new_size:UINTEGER_CPU;

    verify_memory "Begin alloc_size";

    {sz <= minimum_size} ? "Memory: Size bound.";

    new_size  := sz.align_power (POINTER.object_size);

    {(new_size & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";

    ptr_table := table_size (new_size / POINTER.object_size);
    result := micro_alloc new_size table ptr_table lab (new_size.to_uinteger_32);

    verify_memory "End alloc_size";
    result
  );

  - free p:POINTER size sz:UINTEGER_CPU <-
  ( + ptr_table:POINTER;
    + new_size:UINTEGER_CPU;
    {p != NULL} ? "Memory: Pointer NULL.";

    verify_memory "Begin free size";

    new_size  := sz.align_power (POINTER.object_size);

    {(new_size & (POINTER.object_size - 1)) = 0} ? "Memory: Alignment.";

    ptr_table := table_size (new_size / POINTER.object_size);
    micro_free p table ptr_table;

    verify_memory "End free size";
  );

  - alloc_dynamic sz:UINTEGER_CPU :POINTER <-
  // Allocation NATIVE_ARRAY[E]. (LAB_SIZE or malloc)
  ( + new_size,new_size2:UINTEGER_CPU;
    + result:POINTER;
    {sz != 0} ? "Memory: Size = 0";

    verify_memory "Begin alloc_dynamic";

    new_size  := sz.align_power (POINTER.object_size);
    new_size2 := new_size; // + UINTEGER_CPU.object_size;
    (new_size2 <= minimum_size).if {
      result := alloc_size new_size2;
      //put_32 3 to result; // 3 : 2=NATIVE_ARRAY
      result := result; // + UINTEGER_CPU.object_size;
    } else {
      result := search new_size .begin;
    };

    verify_memory "End alloc_dynamic";

    result
  );

  - realloc_dynamic p:POINTER old_size old_sz:UINTEGER_CPU new_size new_sz:UINTEGER_CPU :POINTER <-
  ( + old_size,old_size2,new_size:UINTEGER_CPU;
    + mem:MEMORY;
    + result:POINTER;
    {old_size < new_sz} ? "Memory: New size < Old size.";

    verify_memory "Begin realloc_dynamic";

    old_size  := old_sz.align_power (POINTER.object_size);
    old_size2 := old_size; // + UINTEGER_CPU.object_size;
    new_size  := new_sz.align_power (POINTER.object_size);
    (old_size2 <= minimum_size).if {
      result := alloc_dynamic new_size;
      copy p to result size old_size;
      free (p /*- UINTEGER_CPU.object_size*/) size old_size2;
    } else {
      mem := CONVERT(POINTER, MEMORY).on (p - object_size);
      result := mem.resize new_size index (get_index p).begin;
    };

    verify_memory "End realloc_dynamic";

    result
  );

  - free_dynamic p:POINTER size sz:UINTEGER_CPU <-
  ( + new_size,new_size2:UINTEGER_CPU;
    + mem:MEMORY;

    new_size  := sz.align_power (POINTER.object_size); // BSBS: Optim, alignment by compilo.
    new_size2 := new_size; // + UINTEGER_CPU.object_size;
    (new_size2 <= minimum_size).if {
      free (p /*- UINTEGER_CPU.object_size*/) size new_size2;
    } else {
      mem := CONVERT(POINTER, MEMORY).on (p - object_size);
      mem.to_free (get_index p);
    };
  );

  //
  // GARBAGE COLLECTOR
  //
/*
  - garbage_collector_pass <-
  (
    mark;
    sweep;
  );

Section Private

  - mark <-
  ( + stack,lab:NATIVE_ARRAY(POINTER);
    + ptr,beg_mem,end_mem,ptr_lab,ptr_table,lim_lab:POINTER;
    + size_stack:UINTEGER_32;
    + ptr_mem:UINTEGER_32;
    + mem:MEMORY;
    + type:UINTEGER_32;
    + type_object, size_object,step:UINTEGER_32;

    // Mark Global + BSS.
    mark_static_object;

    // Mark Stack object.
    stack := `top_stack()`:NATIVE_ARRAY(POINTER);
    size_stack := (`@stack - begin_stack`:UINTEGER_32) / POINTER.object_size.to_uinteger_32;
    //
    beg_mem := begin_memory;
    end_mem := beg_mem + (nb_page << 26) - 1;
    size_stack.downto 0 do { i:UINTEGER_32;
      ptr := stack.item i;
      (ptr.in_range beg_mem to end_mem).if {
	// `ptr' is in Store.
	ptr_mem := ptr - begin_memory;
	mem := get_last (ptr_mem >> 26);
	(ptr > mem.next_linear.this).if {
	  // `ptr' is in LAB.
	  ptr_lab := begin_memory + (ptr_mem & 0FFFFF000h);
	  type := read ptr_lab .to_uinteger_32;
	  (type & 01b != 0).if {
	    // LAB Type.
	    type_object := type & 0FFFFFFFEh;
	    size_object := get_object_size type_object;
	    ptr_table   := table_type type_object;
	  } else {
	    // LAB Size.
	    type_object := read ptr .to_uinteger_32 & 0FFFFFFFCh;
	    size_object := type;
	    ptr_table   := table_size (size_object / POINTER.object_size.to_uinteger_32);
	  };
	  lim_lab := read ptr_table - begin_memory;
	  (! ptr_mem.in_range lim_lab to ((lim_lab+4095)&0FFFFF000h)).if {
	    step := ptr - ptr_lab - POINTER.object_size;
	    ((step > 0) && {(step % size_object) = 0}).if {
	      ((read ptr .to_uinteger_32 & 11b) = 0).if {
		mark_object ptr type type_object;
	      };
	    };
	  };
	};
      };
    };
  );

  - sweep <-
  ( + last:MEMORY;
    + ptr_lab,ptr_end,ptr,ptr_table,end_lab,ptr_max:POINTER;
    + type,type_object,size_object:UINTEGER_32;

    // Demark a Global + BSS.
    demark_static_object;

    // Demark a Store.
    ptr_end := begin_memory + 64.mb;
    (nb_page-1).downto 0 do { i:UINTEGER_32;
      last := get_last i;
      ptr_lab := last.next_linear.this;
      {ptr_lab < ptr_end}.while_do {
	type := read ptr_lab .to_uinteger_32;
	ptr  := ptr_lab + POINTER.object_size;
	ptr_lab := ptr_lab + 4096; // Next LAB
	(type & 01b != 0).if {
	  // LAB Type.
	  type_object := type & 0FFFFFFFCh;
	  size_object := get_object_size type_object;
	  ptr_table := table_type type_object;
	  end_lab   := read ptr_table;
	  (((end_lab + 4095) & 0FFFFF000h) = ptr_lab).if {
	    ptr_max := end_lab;
	  } else {
	    ptr_max := ptr_lab;
	  };
	  {ptr < ptr_max}.while_do {
	    demark_object ptr type type_object;
	    ptr := ptr + size_object;
	  };
	} else {
	  // LAB Size.
	  size_object := type;
	  ptr_table := table_size (size_object / POINTER.object_size.to_uinteger_32);
	  end_lab   := read ptr_table;
	  (((end_lab + 4095) & 0FFFFF000h) = ptr_lab).if {
	    ptr_max := end_lab;
	  } else {
	    ptr_max := ptr_lab;
	  };
	  {ptr < ptr_max}.while_do {
	    type_object := read ptr .to_uinteger_32 & 0FFFFFFFCh;
	    demark_object ptr type type_object;
	    ptr := ptr + size_object;
	  };
	};
      };
      ptr_end := ptr_end + 64.mb;
    };
  );
  */

Section Private

  - verify_memory msg:STRING_CONSTANT<-
  ( + m,next,prev:MEMORY;
    + msg_intern:STRING_CONSTANT;

    (is_debug_memory).if {
      (begin_memory != NULL).if {
        next := CONVERT(POINTER, MEMORY).on begin_memory;
        {
          m := next;
          next := m.next_linear;
          prev := m.previous_linear;

          (prev = NULL).if {
            (m != begin_memory).if {
              msg_intern := "Begin list fail!\n";
            };
          } else {
            (prev.next_linear != m).if {
              msg_intern := "Previous fail!\n";
            };
          };
          (m.id = id_busy).if {
            // Busy
          } else {
            // Free
            ((! m.is_end) && {next.id != id_busy}).if {
              msg_intern := "Double free fail!\n";
            };
          };
          (m.is_end).if {

          } else {
            (next.previous_linear != m).if {
              msg_intern := "Next fail!\n";
            };
          };
        }.do_until {(m.is_end) || {msg_intern != NULL}};
        (msg_intern != NULL).if {
          "************* Crash memory *************\n".print;
          msg_intern.print;
          msg.print;
          '\n'.print;
          crash;
        };
      };
    };
  );

  - print_memory <-
  ( + m,next:MEMORY;

    "------------------------------------\n".print;
    "first free : ".print;
    first_free.this.print_hex;
    '\n'.print;

    next := CONVERT(POINTER, MEMORY).on begin_memory;
    {
      m := next;
      next := m.next_linear;

      '['.print;
      (m.previous_linear = NULL).if {
	"NULL".print;
      } else {
        (m.previous_linear.this).print_hex;
      };
      "<-".print;
      m.this.print_hex;
      (m.id = id_busy).if {
	"(B)".print;
      } else {
	"(F)".print;
      };
      "->".print;
      (m.is_end).if {
	"NULL".print;
      } else {
	m.next_linear.this.print_hex;
      };
      ']'.print;
    }.do_until {m.is_end};
    '\n'.print;
    "------------------------------------\n".print;
  );

  - print_capacity <-
  (
    print_memory;
  );
